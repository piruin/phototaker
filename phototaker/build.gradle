/*
 * Copyright 2016 Piruin Panichphol
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id "com.github.dcendents.android-maven" version "1.5"
  id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'com.android.library'

group = rootProject.group
version = rootProject.version
description = rootProject.description

android {
  compileSdkVersion ANDROID_TARGET_SDK
  defaultConfig {
    minSdkVersion ANDROID_MIN_SDK
    targetSdkVersion ANDROID_TARGET_SDK
  }

  compileOptions {
    sourceCompatibility JAVA_VERSION
    targetCompatibility JAVA_VERSION
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    abortOnError true
    checkAllWarnings true
    warningsAsErrors true
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }
}

configurations {
  javadocDeps
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  compileOnly "com.android.support:support-core-utils:$ANDROID_SUPPORT_VERSION"
  compileOnly "com.android.support:support-fragment:$ANDROID_SUPPORT_VERSION"

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:2.23.4'
  testImplementation "com.android.support:support-fragment:$ANDROID_SUPPORT_VERSION"

  javadocDeps "com.android.support:support-core-utils:$ANDROID_SUPPORT_VERSION"
}

tasks.whenTaskAdded { task ->
  if (task.name == 'javadoc')
    task.configure { classpath += configurations.javadocDeps }
}

apply from: 'publish.gradle'
